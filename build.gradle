if (Boolean.valueOf(release) == false) {
    version = version + '-SNAPSHOT'
}
group 'com.github.newnewcoder'

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3'
    }
}
apply plugin: 'com.jfrog.bintray'
apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'findbugs'

findbugs {
    ignoreFailures = true
}
tasks.withType(FindBugs) {
    reports {
        xml.enabled = false
        html.enabled = true
    }
}

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url 'https://dl.bintray.com/palantir/releases'
    }
}

ext {
    springVersion = '4.2.5.RELEASE'
    springBatchVersion = '3.0.7.RELEASE'
    log4jVersion = '2.8.2'
    slf4jVersion = '1.7.25'
}

task sourceJar(type: Jar, dependsOn: classes, description: 'Package source into jar') {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc, description: 'Package doc into jar') {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourceJar, javadocJar
}

dependencies {
    compile "org.springframework:spring-core:$springVersion", "org.springframework:spring-context:$springVersion",
            "org.springframework:spring-jdbc:$springVersion", "org.springframework:spring-context-support:$springVersion"
    compile "org.springframework.batch:spring-batch-core:$springBatchVersion"

    compile 'com.microsoft.sqlserver:mssql-jdbc:6.1.0.jre8'

    compile "org.slf4j:slf4j-api:$slf4jVersion", "org.slf4j:jcl-over-slf4j:$slf4jVersion"

    testCompile 'junit:junit:4.12'
    testCompile 'com.palantir.docker.compose:docker-compose-rule-junit4:0.32.0'
    testCompile "org.springframework:spring-test:$springVersion"
    testCompile "org.apache.logging.log4j:log4j-api:$log4jVersion", "org.apache.logging.log4j:log4j-core:$log4jVersion",
            "org.apache.logging.log4j:log4j-jcl:$log4jVersion", "org.apache.logging.log4j:log4j-slf4j-impl:$log4jVersion"
}

publishing {
    publications {
        MyPublication(MavenPublication) {
            from components.java
            groupId = project.group
            artifactId = project.archivesBaseName
            version = project.version
            artifact sourceJar
            artifact javadocJar
        }
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_API_KEY')
    publications = ['MyPublication']
    pkg {
        repo = 'generic'
        name = 'spring-batch-mssql-bulkcopy'
        userOrg = user
        licenses = ['MIT']
        vcsUrl = 'https://github.com/newnewcoder/spring-batch-mssql-bulkcopy'
        labels = ['spring-batch', 'mssql', 'bulk copy']
        publish = true
        publicDownloadNumbers = true
        version {
            name = project.version
            desc = project.description
            released = new Date()
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}